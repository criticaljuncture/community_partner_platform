#= require components/community_programs_map/community_program_markers

class @CPP.CommunityProgramsMap
  accessToken: 'pk.eyJ1IjoiY3JpdGljYWxqdW5jdHVyZSIsImEiOiJjaXExZjdkdWUwMHg0Zndub255c3I0OGpxIn0.oUGPwQ4zi0LQo9z3qayhEg'

  constructor: (@mapEl, mapConfig)->
    mapboxgl.accessToken = @accessToken
    @mapConfig = @generateMapConfig(mapConfig)

    @createMap()
    @addMapControls()

    markers = new CPP.CommunityProgramMarkers(this.map, @mapConfig)

    @map.on 'load', ->
      markers.add()

    if @mapConfig.debug
      @logEvents()

  generateMapConfig: (config)->
    {
      bearing: config.bearing,
      center: [config.center.lng, config.center.lat],
      debug: false,
      layers: config.layers,
      maxBounds: [
        [config.max_bounds.southwest.lng, config.max_bounds.southwest.lat],
        [config.max_bounds.northeast.lng, config.max_bounds.northeast.lat]
      ],
      style: 'mapbox://styles/criticaljuncture/ciq1hi7yy004dcbm8kd75bkpm',
      zoom: config.zoom
    }

  createMap: ->
    cppMap = this
    @map = new mapboxgl.Map(
      _.extend(@mapConfig, {container: cppMap.mapEl.get(0)})
    )

  addMapControls: ->
    @map.addControl(
      new mapboxgl.NavigationControl(),
      'bottom-right'
    )

  logEvents: ->
    map = @map

    @map.on 'zoom', ->
      console.info 'Zoom: ', map.getZoom()

    @map.on 'move', ->
      console.info 'Bearing: ', map.getBearing()
      console.info 'Center: ', map.getCenter()
      console.info 'Bounds: ', map.getBounds()
